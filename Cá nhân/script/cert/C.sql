/*---------------------------------------------------------- 
MASV: 18120397
HO TEN: NGUYEN DANG HONG HUY
LAB: 03 
NGAY: 06/05/2021
----------------------------------------------------------*/ 
USE [QLSV]
GO

-- INSERT SINHVIEN: MATHAU_MD5
CREATE OR ALTER PROCEDURE SP_INS_SINHVIEN 
	@MASV	NVARCHAR(20),
	@HOTEN	NVARCHAR(100),
	@NGAYSINH	DATETIME,
	@DIACHI NVARCHAR(200),
	@MALOP	VARCHAR(20),
	@TENDN	NVARCHAR(100),
	@MATKHAU	VARCHAR(100)
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @MATKHAU_MD5 VARBINARY(MAX);
	SET @MATKHAU_MD5 = CONVERT(VARBINARY(MAX),HASHBYTES('MD5', @MATKHAU));
	INSERT INTO DBO.SINHVIEN
	VALUES (@MASV, @HOTEN, @NGAYSINH, @DIACHI, @MALOP, @TENDN, @MATKHAU_MD5)
END
GO

-- INSERT NHANVIEN: MATKHAU_SHA1; LUONG_AES256

-- Buoc 1:	Tạo master key
IF NOT EXISTS (SELECT * FROM sys.symmetric_keys WHERE symmetric_key_id = 101)
	CREATE MASTER KEY ENCRYPTION BY PASSWORD = '18120397'
GO
-- Buoc 2: Tạo certificate
IF NOT EXISTS (SELECT * FROM sys.certificates WHERE name = 'NDHHUY')
	CREATE CERTIFICATE NDHHUY WITH SUBJECT = 'NDHHUY'
GO
-- Buoc 3: Tạo symmetric key
IF NOT EXISTS (SELECT * FROM sys.symmetric_keys WHERE name = 'PriKey')
	CREATE SYMMETRIC KEY PriKey  
	WITH ALGORITHM = AES_256  
    ENCRYPTION BY CERTIFICATE NDHHUY
GO

-- Buoc 4:
CREATE OR ALTER PROCEDURE SP_INS_NHANVIEN
	@MANV	VARCHAR(20) ,
	@HOTEN	NVARCHAR(100),
	@EMAIL	VARCHAR (20),
	@LUONG	VARCHAR(100),
	@TENDN	NVARCHAR(100),
	@MATKHAU	VARCHAR(100)
AS
BEGIN
	SET NOCOUNT ON;
	OPEN SYMMETRIC KEY PriKey DECRYPTION BY CERTIFICATE NDHHUY;

	DECLARE @MATKHAU_SHA1 VARBINARY(MAX);
	SET @MATKHAU_SHA1 = CONVERT(VARBINARY(MAX),HASHBYTES('SHA1', @MATKHAU));

	DECLARE @LUONG_AES256 VARBINARY(MAX);
	SET @LUONG_AES256 = ENCRYPTBYKEY(KEY_GUID('PriKey'), CONVERT(VARBINARY(MAX),@LUONG));

	INSERT INTO DBO.NHANVIEN
	VALUES (@MANV, @HOTEN, @EMAIL, @LUONG_AES256, @TENDN, @MATKHAU_SHA1);

	CLOSE SYMMETRIC KEY PriKey;
END
GO

-- SELECT NHANVIEN: DECRYPTBYKEY(LUONG) "LUONGCB"
CREATE OR ALTER PROCEDURE SP_SEL_NHANVIEN
AS
BEGIN
	SET NOCOUNT ON;
	OPEN SYMMETRIC KEY PriKey DECRYPTION BY CERTIFICATE NDHHUY;

	SELECT MANV, HOTEN, EMAIL, CONVERT(VARCHAR(MAX), DECRYPTBYKEY(LUONG)) "LUONGCB"
	FROM NHANVIEN

	CLOSE SYMMETRIC KEY PriKey;
END
GO


-- DATA MẪU
EXEC SP_INS_SINHVIEN '18120397', N'Nguyễn Đặng Hồng Huy', '03/01/2000', N'Gia Lai', NULL, '18120397', 'ndhhuy12345'
GO
EXEC SP_INS_SINHVIEN '18120399', N'Phạm Đức Huy', '07/03/2000', N'Lâm Đồng', NULL, '18120399', 'pdhuy12345'
GO
EXEC SP_INS_SINHVIEN '18120423', N'Trịnh Tấn Khoa', '12/26/2000', N'An Giang', NULL, '18120423', 'ttkhoa12345'
GO

EXEC SP_INS_NHANVIEN 'NV01', N'NGUYEN VAN A', 'nva@yahoo.com', '3000000', 'NVA', '123456'
GO
EXEC SP_INS_NHANVIEN 'NV02', N'NGUYEN VAN B', 'nvb@yahoo.com', '2000000', 'NVB', '1234567'
GO

SELECT * FROM DBO.SINHVIEN
GO 
SELECT * FROM DBO.NHANVIEN
GO 
EXEC SP_SEL_NHANVIEN
GO